module expressions

import analyzer
import ast
import base
import models

@[heap]
pub struct ExprGen {
pub mut:
	model    &models.VType
	analyzer &analyzer.Analyzer
	state    &base.TranslatorState
	target_type string
}

pub fn new_expr_gen(model &models.VType, type_analyzer &analyzer.Analyzer, state &base.TranslatorState) &ExprGen {
	return &ExprGen{
		model:    unsafe { model }
		analyzer: type_analyzer
		state:    state
	}
}

fn (eg &ExprGen) type_ctx() base.TypeGuessingContext {
	return base.TypeGuessingContext{
		type_map:           eg.analyzer.type_map
		location_map:       eg.analyzer.location_map
		known_v_types:      eg.state.known_v_types
		name_remap:         eg.state.name_remap
		defined_classes:    eg.state.defined_classes
		explicit_any_types: eg.analyzer.explicit_any_types
		target_type:        eg.target_type
		analyzer:           eg.analyzer
	}
}

pub fn (eg &ExprGen) guess_type(node ast.Expression) string {
	return base.guess_type(node, eg.type_ctx(), true)
}

pub fn (eg &ExprGen) guess_type_no_loc(node ast.Expression) string {
	return base.guess_type(node, eg.type_ctx(), false)
}

pub fn (mut eg ExprGen) wrap_bool(node ast.Expression, invert bool) string {
	expr := eg.visit(node)
	v_type := eg.guess_type(node)
	return base.wrap_bool(node, expr, v_type, invert)
}

pub fn (eg &ExprGen) indent() string {
	return eg.state.indent()
}

fn (mut eg ExprGen) emit(line string) {
	eg.state.output << '${eg.indent()}${line}'
}

pub fn (mut eg ExprGen) visit(node ast.Expression) string {
	// println('ExprGen Visiting: ' + typeof(node).name)
	match node {
		ast.Name { return eg.visit_name(node) }
		ast.Constant { return eg.visit_constant(node) }
		ast.NoneExpr { return 'none' }
		ast.List { return eg.visit_list(node) }
		ast.Tuple { return eg.visit_tuple(node) }
		ast.Dict { return eg.visit_dict(node) }
		ast.Set { return eg.visit_set(node) }
		ast.BinaryOp { return eg.visit_bin_op(node) }
		ast.UnaryOp { return eg.visit_unary_op(node) }
		ast.Compare { return eg.visit_compare(node) }
		ast.Call { return eg.visit_call(node) }
		ast.Attribute { return eg.visit_attribute(node) }
		ast.Subscript { return eg.visit_subscript(node) }
		ast.Slice { return eg.visit_slice(node) }
		ast.ListComp { return eg.visit_list_comp(node, '') or { '[]' } }
		ast.GeneratorExp { return eg.visit_generator_exp(node, '') or { '[]' } }
		ast.DictComp { return eg.visit_dict_comp(node, '') or { '{}' } }
		ast.SetComp { return eg.visit_set_comp(node, '') or { '{}' } }
		ast.IfExp { return eg.visit_if_exp(node) }
		ast.Starred { return eg.visit_starred(node) }
		ast.JoinedStr { return eg.visit_joined_str(node) }
		ast.FormattedValue { return eg.visit_formatted_value(node) }
		ast.Lambda { return eg.visit_lambda(node) }
		ast.Await { return eg.visit_await(node) }
		ast.Yield { return eg.visit_yield(node) }
		ast.YieldFrom { return eg.visit_yield_from(node) }
		ast.NamedExpr { return eg.visit_named_expr(node) }
		else { return '/* unsupported expr */' }
	}
}

pub fn (mut eg ExprGen) visit_name(node ast.Name) string {
	name := eg.state.name_remap[node.id] or { node.id }
	if name.len > 0 && name[0].is_capital() && !name.is_upper() {
		return base.sanitize_name(name, true, map[string]bool{}, '', map[string]bool{})
	}
	return base.sanitize_name(name, false, map[string]bool{}, '', map[string]bool{})
}

fn (eg &ExprGen) extract_string_content(value string) string {
	if value.len >= 3 && value[0] == `t` && (value[1] == `'` || value[1] == `"`) {
		return value[2..value.len - 1]
	}
	if value.len >= 2 && (value[0] == `'` || value[0] == `"`) {
		return value[1..value.len - 1]
	}
	return value
}

fn (eg &ExprGen) quote_string_content(value string) string {
	if value.len == 0 {
		return "''"
	}

	if value.contains('\\') {
		if !value.contains("'") {
			return "r'${value}'"
		}
		if !value.contains('"') {
			return 'r"${value}"'
		}
	}

	mut escaped := value.replace('\\', '\\\\')
	escaped = escaped.replace("'", "\\'")
	escaped = escaped.replace('\n', '\\n')
	escaped = escaped.replace('\r', '\\r')
	escaped = escaped.replace('\t', '\\t')
	return "'${escaped}'"
}

fn (mut eg ExprGen) translate_tstring(values []ast.Expression) string {
	if values.len == 0 {
		return ''
	}
	if values[0] is ast.Constant {
		first := values[0] as ast.Constant
		if !first.value.starts_with('__py2v_t__')
			&& !first.value.starts_with('t\'')
			&& !first.value.starts_with('t"')
			&& first.token.typ != .tstring_tok {
			return ''
		}

		mut strings := []string{}
		mut interpolations := []string{}

		for i, value in values {
			match value {
				ast.Constant {
					mut content := value.value
					if content.starts_with('__py2v_t__') {
						content = content['__py2v_t__'.len..]
					}
					content = eg.extract_string_content(content)
					strings << eg.quote_string_content(content)
				}
				ast.FormattedValue {
					if i == 0 {
						strings << "''"
					}
					expr_text := eg.visit(value.value)
					conversion := match value.conversion {
						114 {
							eg.state.used_builtins['py_repr'] = true
							"'r'"
						}
						115 { "'s'" }
						97 {
							eg.state.used_builtins['py_ascii'] = true
							"'a'"
						}
						else { "'none'" }
					}
					mut format_spec := "''"
					if format_spec_node := value.format_spec {
						format_spec = eg.visit(format_spec_node)
					}
					interpolations << 'Interpolation{value: ${expr_text}, expression: ${eg.quote_string_content(expr_text)}, conversion: ${conversion}, format_spec: ${format_spec}}'
					if i == values.len - 1 {
						strings << "''"
					}
				}
				else {}
			}
		}

		for strings.len < interpolations.len + 1 {
			strings << "''"
		}

		return 'Template{strings: [${strings.join(', ')}], interpolations: [${interpolations.join(', ')}]}'
	}
	return ''
}

pub fn (mut eg ExprGen) visit_constant(node ast.Constant) string {
	if node.value == 'None' {
		return 'none'
	}
	if node.value == 'True' {
		return 'true'
	}
	if node.value == 'False' {
		return 'false'
	}
	if node.value.starts_with("b'") || node.value.starts_with('b"') {
		content := eg.extract_string_content(node.value[1..])
		return eg.bytes_literal_to_v(content)
	}
	if node.token.typ == .tstring_tok || node.value.starts_with('__py2v_t__')
		|| node.value.starts_with('t\'') || node.value.starts_with('t"') {
		mut content := node.value
		if content.starts_with('__py2v_t__') {
			content = content['__py2v_t__'.len..]
		}
		content = eg.extract_string_content(content)
		return 'Template{strings: [${eg.quote_string_content(content)}], interpolations: []}'
	}
	if node.token.typ == .string_tok || node.token.typ == .fstring_tok {
		if node.value.starts_with("'") || node.value.starts_with('"') || node.value.starts_with('t\'')
			|| node.value.starts_with('t"') {
			return node.value
		}
		return "'${node.value}'"
	}
	if node.value.ends_with('j') && !node.value.starts_with("'") && !node.value.starts_with('"') {
		content := node.value[..node.value.len - 1]
		val := if content.contains('.') { content } else { '${content}.0' }
		eg.state.used_builtins['py_complex'] = true
		return 'py_complex(0.0, ${val})'
	}
	return node.value
}

fn (eg &ExprGen) bytes_literal_to_v(content string) string {
	if content.len == 0 {
		return '[]u8{}'
	}
	mut items := []string{}
	mut i := 0
	for i < content.len {
		ch := content[i]
		if ch == `\\` && i + 1 < content.len {
			next := content[i + 1]
			if next == `x` && i + 3 < content.len {
				hex := content[i + 2..i + 4]
				items << 'u8(0x${hex})'
				i += 4
				continue
			}
			match next {
				`n` { items << 'u8(0x0a)' }
				`r` { items << 'u8(0x0d)' }
				`t` { items << 'u8(0x09)' }
				`\\` { items << 'u8(0x5c)' }
				`'` { items << 'u8(0x27)' }
				`"` { items << 'u8(0x22)' }
				else { items << 'u8(0x${next.hex()})' }
			}
			i += 2
			continue
		}
		items << 'u8(0x${ch.hex()})'
		i++
	}
	return '[${items.join(', ')}]'
}

pub fn (mut eg ExprGen) visit_list(node ast.List) string {
	mut has_starred := false
	for elt in node.elements {
		if elt is ast.Starred {
			has_starred = true
			break
		}
	}
	
	mut values := []string{}
	for elt in node.elements {
		values << eg.visit(elt)
	}
	
	if has_starred {
		eg.state.used_list_concat = true
		mut args := []string{}
		for elt in node.elements {
			val := eg.visit(elt)
			if elt is ast.Starred {
				// Remove '...' if it was added by visit_starred
				if val.starts_with('...') {
					args << val[3..]
				} else {
					args << val
				}
			} else {
				args << '[${val}]'
			}
		}
		return 'py_list_concat([${args.join(', ')}])'
	}
	
	if values.len == 0 {
		mut list_type := if eg.target_type.starts_with('[]') { eg.target_type } else { eg.guess_type(node) }
		if eg.target_type == 'Any' { list_type = '[]Any' }
		if list_type.starts_with('[]') {
			return '${list_type}{}'
		}
		return '[]'
	}
	if eg.target_type == 'Any' {
		return '[]Any{${values.join(', ')}}'
	}
	return '[${values.join(', ')}]'
}

pub fn (mut eg ExprGen) visit_tuple(node ast.Tuple) string {
	mut has_starred := false
	for elt in node.elements {
		if elt is ast.Starred {
			has_starred = true
			break
		}
	}
	
	mut values := []string{}
	for elt in node.elements {
		values << eg.visit(elt)
	}
	
	if has_starred {
		eg.state.used_list_concat = true
		mut args := []string{}
		for elt in node.elements {
			val := eg.visit(elt)
			if elt is ast.Starred {
				if val.starts_with('...') {
					args << val[3..]
				} else {
					args << val
				}
			} else {
				args << '[${val}]'
			}
		}
		return 'py_list_concat([${args.join(', ')}])'
	}
	
	return '[${values.join(', ')}]'
}

pub fn (mut eg ExprGen) visit_dict(node ast.Dict) string {
	dict_type := if eg.target_type.len > 0 { eg.target_type } else { 'Any' }
	is_struct := dict_type in eg.state.defined_classes

	if node.keys.len == 0 {
		if is_struct { return "${dict_type}{}" }
		if dict_type.starts_with("map[") {
			return "${dict_type}{}"
		}
		return "map[string]Any{}"
	}
	mut items := []string{}
	for i, key in node.keys {
		if i >= node.values.len {
			break
		}
		val := eg.visit(node.values[i])
		if is_struct && key is ast.Constant && (key.token.typ == .string_tok || key.token.typ == .fstring_tok) {
			items << '${key.value.trim('\'"')}: ${val}'
			continue
		}
		if key is ast.NoneExpr {
			items << val
			continue
		}
		items << '${eg.visit(key)}: ${val}'
	}
	if is_struct {
		return '${dict_type}{${items.join(', ')}}'
	}
	return '{${items.join(', ')}}'
}

pub fn (mut eg ExprGen) visit_set(node ast.Set) string {
	mut items := []string{}
	for elt in node.elements {
		items << eg.visit(elt)
	}
	if items.len == 0 {
		eg.state.used_builtins['datatypes'] = true
		return 'datatypes.Set[Any]{}'
	}

	mut inner_type := eg.guess_type(node.elements[0])
	if inner_type == 'str' { inner_type = 'string' }
	if inner_type == 'Any' || inner_type == 'unknown' { inner_type = 'int' } // fallback for test literal {1, 2}

	mut elts := []string{}
	for it in items {
		elts << '${it}: true'
	}

	eg.state.used_builtins['datatypes'] = true
	return 'datatypes.Set[${inner_type}]{elements: {${elts.join(', ')}}}'
}

pub fn (mut eg ExprGen) visit_slice(node ast.Slice) string {
	mut lower := ''
	if lower_expr := node.lower {
		lower = eg.visit(lower_expr)
	}
	mut upper := ''
	if upper_expr := node.upper {
		upper = eg.visit(upper_expr)
	}
	if step_expr := node.step {
		step := eg.visit(step_expr)
		return '${lower}..${upper};${step}'
	}
	return '${lower}..${upper}'
}

pub fn (mut eg ExprGen) visit_joined_str(node ast.JoinedStr) string {
	tstring := eg.translate_tstring(node.values)
	if tstring.len > 0 {
		return tstring
	}
	mut res := "'"
	for value in node.values {
		if value is ast.Constant {
			res += eg.extract_string_content(value.value)
		} else if value is ast.FormattedValue {
			res += '$' + '{' + eg.visit(value.value) + '}'
		}
	}
	res += "'"
	return res
}

pub fn (mut eg ExprGen) visit_formatted_value(node ast.FormattedValue) string {
	expr := eg.visit(node.value)
	expr_type := eg.guess_type(node.value)
	if node.conversion == 114 {
		eg.state.used_builtins['py_repr'] = true
	} else if node.conversion == 97 {
		eg.state.used_builtins['py_ascii'] = true
	}
	if node.conversion == 114 || node.conversion == 115 || node.conversion == 97
		|| node.format_spec != none || expr_type !in ['string', 'LiteralString'] {
		return 'string(${expr})'
	}
	return expr
}

pub fn (mut eg ExprGen) visit_lambda(node ast.Lambda) string {
	mut params := []string{}
	mut param_types := map[string]string{}
	for arg in node.args.posonlyargs {
		typ := eg.lambda_param_type(arg.annotation)
		params << "${arg.arg} ${typ}"
		param_types[arg.arg] = typ
	}
	for arg in node.args.args {
		typ := eg.lambda_param_type(arg.annotation)
		params << "${arg.arg} ${typ}"
		param_types[arg.arg] = typ
	}
	for arg in node.args.kwonlyargs {
		typ := eg.lambda_param_type(arg.annotation)
		params << "${arg.arg} ${typ}"
		param_types[arg.arg] = typ
	}
	if va := node.args.vararg {
		params << "${va.arg} []Any"
		param_types[va.arg] = "[]Any"
	}
	ret_type := eg.lambda_return_type(node.body, param_types)
	mut val := eg.visit(node.body)
	args_str := params
	if ret_type in ["none", "void", "None"] {
		if val == "none" { val = "" }
		body_s := if val.len > 0 { " ${val} " } else { "" }
		return "fn (${args_str.join(', ')}) {${body_s}}"
	}
	return "fn (${args_str.join(', ')}) ${ret_type} { return ${val} }"
}

fn (mut eg ExprGen) lambda_param_type(annotation ?ast.Expression) string {
	if ann := annotation {
		if ann is ast.Name {
			return match ann.id {
				'float' { 'f64' }
				'int' { 'int' }
				'str' { 'string' }
				'bool' { 'bool' }
				else { eg.visit(ann) }
			}
		}
		return eg.visit(ann)
	}
	return 'int'
}

fn (eg &ExprGen) lambda_return_type(body ast.Expression, param_types map[string]string) string {
	mut ctx := eg.type_ctx()
	for k, v in param_types {
		ctx.type_map[k] = v
	}
	ret_type := base.guess_type(body, ctx, true)
	return if ret_type in ["Any", "void", "unknown"] { "Any" } else { ret_type }
}

pub fn (mut eg ExprGen) visit_await(node ast.Await) string {
	return '/* await */ ${eg.visit(node.value)}'
}

pub fn (mut eg ExprGen) visit_yield(node ast.Yield) string {
	if eg.state.coroutine_handler != unsafe { nil } {
		mut ch := unsafe { &analyzer.CoroutineHandler(eg.state.coroutine_handler) }
		if act_ch := ch.active_channel {
			in_ch := ch.active_in_channel or { '/* no in_ch */' }
			val := if v := node.value { eg.visit(v) } else { 'none' }
			eg.state.used_builtins['py_yield'] = true
			return 'py_yield(${act_ch}, ${in_ch}, ${val})'
		}
	}
	return '/* yield outside generator */'
}

pub fn (mut eg ExprGen) visit_yield_from(node ast.YieldFrom) string {
	val := eg.visit(node.value)
	return '/* yield from not fully supported */ ${val}'
}

pub fn (mut eg ExprGen) visit_named_expr(node ast.NamedExpr) string {
	target := eg.visit(node.target)
	value := eg.visit(node.value)
	return '(${target} = ${value})'
}
pub fn (mut eg ExprGen) map_python_type(type_str string, is_return bool) string {
	return eg.map_type_ext(type_str, false, true, is_return)
}

pub fn (mut eg ExprGen) map_type_ext(type_str string, allow_union bool, register bool, is_return bool) string {
	opts := base.TypeMapOptions{
		struct_name:        eg.state.current_class
		allow_union:        allow_union
		register_sum_types: register
		is_return:          is_return
		generic_map:        eg.state.current_class_generic_map
	}
	mut ctx := base.TypeUtilsContext{
		imported_symbols: eg.state.imported_symbols
		scc_files:        eg.state.scc_files.keys()
		used_builtins:    eg.state.used_builtins
		warnings:         eg.state.warnings
		config:           eg.state.config
	}
	mut actual_struct := if opts.struct_name.len > 0 && opts.struct_name != 'Self' { opts.struct_name } else { eg.state.current_class }
	if actual_struct == '' { actual_struct = 'Self' }

	return base.map_type(type_str, opts, mut ctx, fn [mut eg, actual_struct] (name string) string {
		if name == 'Self' || name == 'typing.Self' {
			mut v_gens := []string{}
			for gn in eg.state.current_class_generics {
				v_gens << eg.state.current_class_generic_map[gn] or { gn }
			}
			gen_s := if v_gens.len > 0 { "[${v_gens.join(', ')}]" } else { "" }
			return "&" + actual_struct + gen_s
		}
		if name.contains("|") {
			eg.state.generated_sum_types[name] = ''
			return name
		}
		return ""
	}, noop_literal_registrar, noop_tuple_registrar)
}

fn noop_sum_type_registrar(_ string) string {
	return ''
}

fn noop_literal_registrar(_ []string) string {
	return ''
}

fn noop_tuple_registrar(_ string) string {
	return ''
}
